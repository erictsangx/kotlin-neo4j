plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "com.adarshr.test-logger" version "1.6.0"
    id 'maven'
    id 'signing'
}


group = 'io.github.erictsangx'
archivesBaseName = "kotlin-neo4j"
version = "0.0.6"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.slf4j:slf4j-api:1.7.26'

    implementation 'org.neo4j.driver:neo4j-java-driver:1.7.3'

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testImplementation "io.github.erictsangx:kotlin-random:0.0.1"
    testImplementation 'io.github.microutils:kotlin-logging:1.4'

    testImplementation 'ch.qos.logback:logback-core:1.1.7'
    testImplementation 'ch.qos.logback:logback-classic:1.1.7'

    testImplementation "org.neo4j:neo4j-slf4j:3.5.4"
    testImplementation "org.neo4j.community:it-test-support:3.5.4"

    testImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


test {
    useJUnitPlatform()
}

testlogger {
    theme 'mocha'
    showExceptions true
    slowThreshold 2000
    showSummary true
    showPassed true
    showSkipped true
    showFailed true
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "${project.group}:${project.archivesBaseName}"
                description "kotlin-neo4j is a wrapper of 'org.neo4j.driver:neo4j-java-driver' and written by Kotlin."
                url "https://github.com/erictsangx/kotlin-neo4j"

                scm {
                    connection 'scm:git:git://github.com/erictsangx/kotlin-neo4j.git'
                    developerConnection 'scm:git:ssh://github.com/erictsangx/kotlin-neo4j.git'
                    url 'https://github.com/erictsangx/kotlin-neo4j'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'erictsangx'
                        name 'Eric Tsang'
                        email 'erictsangx@gmail.com'
                    }
                }
            }
        }
    }
}
